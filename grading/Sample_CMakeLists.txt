cmake_minimum_required (VERSION 2.8)
project (sample_assignment)

###################################################################################

# these variables will be replaced by INSTALL.sh
set (SUBMITTY_INSTALL_DIR __INSTALL__FILLIN__SUBMITTY_INSTALL_DIR__)
set (SUBMITTY_DATA_DIR __INSTALL__FILLIN__SUBMITTY_DATA_DIR__)

# assuming the core files for config, compilation, running, & grading are here:
set (GRADINGCODE  ${SUBMITTY_INSTALL_DIR}/src)
set (JSONCODE     ${SUBMITTY_INSTALL_DIR}/vendor/include)
set (GRADINGLIB   ${SUBMITTY_INSTALL_DIR}/src/grading/lib/libsubmitty_grading.a)

###################################################################################

# DEFAULT:  NORMAL ASSIGNMENT INSTALLATION
# NON-DEFAULT:  TEST SUITE INSTALLATION
option(ASSIGNMENT_INSTALLATION "Normal assignment installation" ON)

set (MY_DIR ${CMAKE_SOURCE_DIR})

IF (ASSIGNMENT_INSTALLATION)
  # assuming we are in a directory of this structure
  #   SUBMITTY_DATA_DIR/courses/which_semester/which_course/build/which_assignment

  # base directory for course
  set (course_dir ${MY_DIR}/../..)
  #alternately/equivalently
  #set (course_dir ${SUBMITTY_DATA_DIR}/courses)

  # just the last part of the current directory
  get_filename_component(assignment ${MY_DIR} NAME)
  message("ASSIGNMENT IS ${assignment}")
  string(TIMESTAMP mytime "%Y/%m/%d %H:%M:%S")
  message("BUILD TIME IS ${mytime}")

  # Where the compiled binaries are stored, under the class directory
  set(my_bin ${course_dir}/bin/${assignment})

  # Assignment configuration json file location
  set(my_hw_config_file ${course_dir}/config/build/build_${assignment}.json)

else (ASSIGNMENT_INSTALLATION)
  # Otherwise, this is part of the test suite....
  # base directory for test suite is the immediate parent directory
  get_filename_component(which_test_dir ${MY_DIR} DIRECTORY)

  # the name of the test is the name of the parent directory
  get_filename_component(which_test ${which_test_dir} NAME)
  message("WHICH TEST: ${which_test}")

  include_directories(${which_test_dir}/assignment_config)

  # Where the compiled binaries are stored
  set(my_bin ${which_test_dir}/bin)
  message("this is where i'll put the executables ${my_bin}")

  # configuration json file location
  # set(my_hw_config_file ${which_test_dir}/data/assignment_config.json)
  set(my_hw_config_file ${which_test_dir}/data/build_config.json)

endif(ASSIGNMENT_INSTALLATION)


###################################################################################
# OPTIONAL INSTRUCTOR CUSTOMIZATION
#
# If extra .cpp files are needed for grading/validation, add a file
#  named "instructor_CMakeLists.txt" and in that file list the .cpp
#  files in the variable "custom_files".  For example:
#
#  set (custom_files   custom_validation_code/custom.cpp  custom_validation_code/helper.cpp)
#

include(instructor_CMakeLists.txt OPTIONAL RESULT_VARIABLE instructor_cmakelists)

if (instructor_cmakelists)
message("DETECTED INSTRUCTOR CUSTOMIZATIONS in ${instructor_cmakelists}")
message("  custom_files: ${custom_files}")
else()
set (custom_files ${GRADINGCODE}/grading/empty_custom_function.cpp)
endif(instructor_cmakelists)


###################################################################################

# Read in the complete config json
file(READ ${my_hw_config_file} complete_config_obj)

# Then embed the config.json within a generated .cpp source file
string(REGEX REPLACE "([\"\\\n])" "\\\\\\1" complete_config_obj "${complete_config_obj}")
set(cpp_header "const char *GLOBAL_config_json_string = \"")
set(cpp_footer "\";\n")
file(WRITE json_generated.cpp "${cpp_header}${complete_config_obj}${cpp_footer}")


###################################################################################
# source files

# build this as a library, so we only have to compile it once
add_library(custom_functions
  ${GRADINGCODE}/grading/seccomp_functions.cpp
  json_generated.cpp
  ${custom_files}
)

add_executable(compile.out
  ${GRADINGCODE}/grading/main_compile.cpp
)

add_executable(run.out
  ${GRADINGCODE}/grading/main_runner.cpp
)

add_executable(validate.out
  ${GRADINGCODE}/grading/main_validator.cpp
)



###################################################################################

SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -pthread")

target_link_libraries(custom_functions seccomp)
target_link_libraries(compile.out                ${GRADINGLIB}  seccomp  custom_functions)
target_link_libraries(run.out                    ${GRADINGLIB}  seccomp  custom_functions)
target_link_libraries(validate.out               ${GRADINGLIB}  seccomp  custom_functions)

set_property(TARGET custom_functions         APPEND_STRING PROPERTY COMPILE_FLAGS "-g -std=c++11")
set_property(TARGET compile.out              APPEND_STRING PROPERTY COMPILE_FLAGS "-g -std=c++11")
set_property(TARGET run.out                  APPEND_STRING PROPERTY COMPILE_FLAGS "-g -std=c++11")
set_property(TARGET validate.out             APPEND_STRING PROPERTY COMPILE_FLAGS "-g -std=c++11")

include_directories(${MY_DIR})
include_directories(${GRADINGCODE})
include_directories(${GRADINGCODE}/grading)
include_directories(${JSONCODE})

SET(EXECUTABLE_OUTPUT_PATH ${my_bin})

###################################################################################

